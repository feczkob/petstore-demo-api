import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

plugins {
    id 'java'
    id "org.openapi.generator" version "7.17.0"
}

group 'com.petstore'
version '0.1'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

def petstoreApiYml = "$rootDir/src/main/resources/petstore-api.yml".toString()

/* java client configuration */
openApiGenerate {
    generatorName = "java"
    inputSpec = petstoreApiYml
    outputDir = "$buildDir/java-client".toString()

    groupId = "$project.group"
    id = "$project.name-java-client"
    version = "$project.version"
    apiPackage = "com.petstore.api"
    invokerPackage = "com.petstore.api.invoker"
    modelPackage = "com.petstore.api.model"
    enablePostProcessFile = true
    skipOverwrite = false
    configOptions = [
            java8               : "true",
            dateLibrary         : "java8",
            serializationLibrary: "jackson",
            library             : "resttemplate",
            useBeanValidation   : "true",
            enableBuilderSupport: "true"
    ]
    globalProperties = [
            modelDocs: "true"
    ]
}

tasks.register('generateCode', Copy) {
    dependsOn tasks.named('openApiGenerate')

    def srcPath = "$buildDir/java-client"
    def targetPath = "$rootDir/docs"

    // copy docs
    from(srcPath + "/docs")
    into(targetPath)
    include('*.md')
}

// code will be generated during compilation
tasks.named('compileJava') {
    dependsOn tasks.named('generateCode')
}


// attach the generated folder as source for this project
sourceSets {
    main {
        java {
            srcDir "$buildDir/java-client/src/main/java"
        }
    }
}


// these dependencies are required for the generated code in order to build
ext {
    swagger_annotations_version = "1.6.14"
    jackson_version = "2.17.2"
    jackson_databind_version = "2.17.2"
    spring_web_version = "5.3.39"
    jodatime_version = "2.12.7"
    junit_version = "4.13.2"
    validation_version = "2.0.1.Final"
    jackson_nullable_version = "0.2.6"
}

dependencies {
    implementation "io.swagger:swagger-annotations:$swagger_annotations_version"
    implementation "com.google.code.findbugs:jsr305:3.0.2"
    implementation "org.springframework:spring-web:$spring_web_version"
    implementation "org.springframework:spring-context:$spring_web_version"
    implementation "com.fasterxml.jackson.core:jackson-core:$jackson_version"
    implementation "com.fasterxml.jackson.core:jackson-annotations:$jackson_version"
    implementation "com.fasterxml.jackson.core:jackson-databind:$jackson_databind_version"
    implementation "com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:$jackson_version"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jackson_version"
    implementation "org.openapitools:jackson-databind-nullable:$jackson_nullable_version"
    testImplementation "junit:junit:$junit_version"
    implementation group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'
    implementation "javax.validation:validation-api:$validation_version"
}
